-- Za https://www.mimuw.edu.pl/~ben/Zajecia/Mrj2019/Latte/

-- Programs ------------------------------------------------

entrypoints Program ;

Prog.       Program ::= [VDecl] [FDef] ;

-- Definitions & Declarations ---------------------------------------------

FBody.      FBody ::= "{" [VDecl] [Stmt] Return "}" ;

RetVal.    	Return ::= "return" Expr ";" ;

RetVoid.    Return ::= "return" ";" ;

DFun.       FDef ::= Type Ident "(" [Arg] ")" FBody ;

separator   nonempty FDef "" ;

ArgVal.     Arg ::= Type Ident ;

ArgRef.     Arg ::= Type "&" Ident ;

separator   Arg "," ;

DVar.       VDecl ::= Type Item ";";

separator   VDecl "" ;

NoInit.     Item ::= Ident ;

Init.       Item ::= Ident "=" Expr ;

-- Types ---------------------------------------------------

TInt.       Type ::= "int" ;

TString.    Type ::= "string" ;

TBool.      Type ::= "bool" ;

TVoid.      Type ::= "void" ;

-- Statements ----------------------------------------------

Blk.        Block ::= "{" [VDecl] [Stmt] "}" ;

SBlock.     Stmt ::= Block ;

SEmpty.     Stmt ::= ";" ;

SAssign.    Stmt ::= Ident "=" Expr  ";" ;

SCond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

SCondElse.  Stmt ::= "if" "(" Expr ")" Block "else" Block  ;

SWhile.     Stmt ::= "while" "(" Expr ")" Stmt ;

SExp.       Stmt ::= Expr ";" ;

separator   Stmt "" ;

-- Expressions ----------------------------------------------

EVar.       Expr6 ::= Ident ;

ELitInt.    Expr6 ::= Integer ;

ELitTrue.   Expr6 ::= "true" ;

ELitFalse.  Expr6 ::= "false" ;

EApp.       Expr6 ::= Ident "(" [Expr] ")" ;

EString.    Expr6 ::= String ;

ENeg.       Expr5 ::= "-" Expr6 ;

ENot.       Expr5 ::= "!" Expr6 ;

EMul.       Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.       Expr3 ::= Expr3 AddOp Expr4 ;

ECmp.       Expr2 ::= Expr2 CmpOp Expr3 ;

EAnd.       Expr1 ::= Expr2 "&&" Expr1 ;

EOr.        Expr ::= Expr1 "||" Expr ;

coercions   Expr 6 ;

separator   Expr "," ;

-- Operators -----------------------------------------------

OPlus.      AddOp ::= "+" ;

OMinus.     AddOp ::= "-" ;

OTimes.     MulOp ::= "*" ;

ODiv.       MulOp ::= "/" ;

OMod.       MulOp ::= "%" ;

-- Comparisons ---------------------------------------------

CLTH.       CmpOp ::= "<" ;

CLEQ.       CmpOp ::= "<=" ;

CGTH.       CmpOp ::= ">" ;

CGEQ.       CmpOp ::= ">=" ;

CEQU.       CmpOp ::= "==" ;

CNEQ.       CmpOp ::= "!=" ;

-- Comments ------------------------------------------------

comment     "//" ;

comment     "/*" "*/" ;
